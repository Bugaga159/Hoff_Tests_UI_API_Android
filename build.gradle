plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.9.6'
    id "io.freefair.lombok" version "6.4.3"
}

repositories {
    mavenCentral()
}

def allureVersion = "2.17.3",
    selenideVersion = "6.5.0",
    junitVersion = "5.8.2",
    appiumVersion = "8.0.0",
    ownerVersion = "1.0.12"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
//    testCompileOnly 'org.projectlombok:lombok:1.18.24'
//    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
    testImplementation(
            "org.aspectj:aspectjweaver:1.9.9.1",
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.appium:java-client:$appiumVersion",
            "io.rest-assured:rest-assured:5.0.1",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "commons-io:commons-io:2.11.0",
            "org.assertj:assertj-core:3.22.0",
            "com.fasterxml.jackson.core:jackson-core:2.13.3",
            "com.fasterxml.jackson.core:jackson-databind:2.13.3",
            "com.github.javafaker:javafaker:1.0.2",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.junit.jupiter:junit-jupiter-params:$junitVersion",
            "org.slf4j:slf4j-simple:1.7.36")
}
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task runTestsUi(type: Test) {
    useJUnitPlatform {
        includeTags 'UI'
    }
}

task runTestsApi(type: Test) {
    useJUnitPlatform {
        includeTags 'API'
    }
}

task runTestsMobile(type: Test) {
    useJUnitPlatform {
        includeTags 'Android'
    }
}
